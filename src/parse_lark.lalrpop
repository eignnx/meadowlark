use lalrpop_util::ParseError;

use lark_vm::cpu::regs::Reg;
use crate::{UserError, ast::{const_val::*, lvalue::*, rvalue::*}, ast::*};

grammar;

extern {
    type Error = UserError;
    type Location = usize;
}

match {
    r"\s+" => { },
    r"#[^\n\r]*[\n\r]*" => { }, // comments begin with #
    _,
}

pub Program: Vec<Item> = {
    <items: Item*> => items,
};


SubrOrIsr: bool = {
    "subr" => false,
    "isr" => true,
};

// Regular identifiers must begin with a lowercase letter (optionally preceded by a dot).
Ident: String = <id: r#"\.?_?[a-z][a-zA-Z0-9_.]*"#> => id.to_string();
// Constants must be all uppercase.
ConstIdent: String = <id: r#"[A-Z][A-Z0-9_]*"#> => id.to_string();

ConstValueAtomic: ConstValue = {
    <uint: Unsigned> => ConstValue::Uint(uint),
    <int: Signed> => ConstValue::Int(int),
    <char_lit: CharLit> => ConstValue::Char(char_lit),
    <const_alias: ConstIdent> => ConstValue::ConstAlias(const_alias),
    "(" <value: ConstValue>  ")" => value,
};

ConstValue: ConstValue = {
    <first: ConstValueAtomic> <rest: (BinOp ConstValueAtomic)*> => {
        // We want left associativity and same precedence for all ops.
        rest.into_iter().fold(first, |lhs, (op, rhs)| {
            ConstValue::BinOp(Box::new(lhs), op, Box::new(rhs))
        })
    }
};

BinOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
    "%" => BinOp::Mod,
    "&" => BinOp::And,
    "|" => BinOp::Or,
    "^" => BinOp::Xor,
    "<<" => BinOp::Shl,
    ">>" => BinOp::Shr,
};


Item: Item = {
    "def" <name: ConstIdent> "=" <value: ConstValue> ";" => Item::Const { name, value },
    <is_isr: SubrOrIsr> <subr_name: Ident> "("
        <args: (AliasBinding ("," <AliasBinding>)*)?>
    ")"
    <save_regs: ("preserve" "("
        <(Reg ("," <Reg>)*)?>
    ")")?>
    "{" <body: Stmt*> "}" => Item::SubrDef {
        name: subr_name,
        is_isr: is_isr,

        args: args.map(|(first, rest)| {
            std::iter::once(first).chain(rest.into_iter()).collect()
        }).unwrap_or_default(),

        preserve_regs: save_regs.map(|opt| {
            match opt {
                Some((first, rest)) => {
                    std::iter::once(first).chain(rest.into_iter()).collect()
                },
                None => vec![],
            }
        }).unwrap_or_default(),

        body,
    },
    Directive => Item::Directive(<>),
}

Stmt: Stmt = {
    "restore" ";" => Stmt::Restore,
    "preserve" "(" <regs: (Reg ("," <Reg>)*)> ")" ";" => {
        let (first, rest) = regs;
        Stmt::Preserve(
            std::iter::once(first).chain(rest.into_iter()).collect(),
        )
    },
    "alias" <binding: AliasBinding> ";" => Stmt::DefAlias(binding),
    <instr: Instr> ";" => Stmt::Instr(instr),
    <label: Ident> ":" => Stmt::Label(label.to_string()),
    <if_stmt: IfStmt> => if_stmt,
    <while_loop: WhileLoop> => while_loop,
    "loop" "{" <body: Stmt*> "}" => Stmt::Loop { body },
};

AliasBinding: AliasBinding = {
    <name: Ident> "=>" <lvalue: LValue> => AliasBinding::ExplicitAlias(name.to_string(), lvalue),
    <name: Ident> "=>" "{" <field_bindings: (<AliasBinding> ";")*> "}" => AliasBinding::Struct {
        var_name: name.to_string(),
        field_bindings,
    },
    <name: Ident> => AliasBinding::ImplicitAlias(name.to_string()),
};

IfStmt: Stmt = {
    "if" <test_reg: RValue> "do" "{"
        <then_body: Stmt*>
    "}" <else_body: ("else" "{"
         <Stmt*>
    "}")?> => Stmt::If {
        test_reg,
        test_cond: vec![],
        consequent: then_body,
        alternative: else_body,
    },
    "if" <test_reg: RValue> "("
        <first: Instr> ";" <rest_test_cond: RestInstrSeq?>
    ")" "do" "{"
        <then_body: Stmt*>
    "}" <else_body: ("else" "{"
         <Stmt*>
    "}")?> => Stmt::If {
        test_reg,
        test_cond: std::iter::once(first).chain(rest_test_cond.into_iter().flatten()).collect(),
        consequent: then_body,
        alternative: else_body,
    },
};

WhileLoop: Stmt = {
    "while" <test_arg: RValue> "do" "{" <body: Stmt*> "}" => Stmt::While {
        test_arg,
        test_cond: vec![],
        update: None,
        body,
    },

    "while" <test_arg: RValue> "("
        <first: Instr> ";" <rest_test_cond: RestInstrSeq?>
    ")" "do" "{" <body: Stmt*> "}" => Stmt::While {
        test_arg,
        test_cond: std::iter::once(first).chain(rest_test_cond.into_iter().flatten()).collect(),
        update: None,
        body,
    },

    "while" <test_arg: RValue> "update" "("
        <update_first: Instr> ";" <update_rest: RestInstrSeq?>
    ")" "do" "{" <body: Stmt*> "}" => Stmt::While {
        test_arg,
        test_cond: vec![],
        update: Some(std::iter::once(update_first).chain(update_rest.into_iter().flatten()).collect()),
        body,
    },

    "while" <test_arg: RValue> "("
        <first: Instr> ";" <rest_test_cond: RestInstrSeq?>
    ")" "update" "("
        <update_first: Instr> ";" <update_rest: RestInstrSeq?>
    ")" "do" "{" <body: Stmt*> "}" => Stmt::While {
        test_arg,
        test_cond: std::iter::once(first).chain(rest_test_cond.into_iter().flatten()).collect(),
        update: Some(std::iter::once(update_first).chain(update_rest.into_iter().flatten()).collect()),
        body,
    },
};

RestInstrSeq: Vec<Instr> = {
    <instrs: (<Instr> ";")+> => instrs,
};

Instr: Instr = {
    <op: OpName> => Instr { op, args: vec![] },
    <op: OpName> <fst: RValue> <rest: ("," <RValue>)*> => {
        let args = std::iter::once(fst).chain(rest.into_iter()).collect();
        Instr { op, args }
    },
};

OpName: String = <op: r"![a-z]+"> => op[1..].to_string();

RValue: RValue = {
    <imm: Integer> => imm,
    <char_literal: CharLit> => RValue::Char(char_literal),
    <string_literal: r#""[^"]*""#> => {
        RValue::String(string_literal[1..string_literal.len()-1].to_string())
    },
    "&" <label: Ident> => RValue::Label(label.to_string()),
    <const_name: ConstIdent> => RValue::ConstAlias(const_name.to_string()),
    <lvalue: LValue> => RValue::LValue(lvalue),
};

CharLit: u8 = {
    <l: @L> <char_content: r"'[^']+'"> <r: @R> =>? Ok(
        match &char_content[1..char_content.len()-1] {
            r"\n" => b'\n',
            r"\t" => b'\t',
            r"\r" => b'\r',
            r"\'" => b'\'',
            r"\\" => b'\\',
            r"\0" => b'\0',
            empty if empty.is_empty() => return Err(
                ParseError::User {
                    error: UserError {
                        msg: "character literal cannot be empty".to_string(),
                        span: (l, r),
                    }
                }
            ),
            text if text.len() == 1 => text.bytes().next().unwrap(),
            text => return Err(
                ParseError::User {
                    error: UserError {
                        msg: "character literal must be a single character".to_string(),
                        span: (l, r),
                    }
                }
            ),
        }
    ),
};

LValue: LValue = {
    <reg: Reg> => LValue::Reg(reg),
    <alias_name: Ident> => LValue::Alias(alias_name.to_string()),
    "[" <base: Base> "]" => LValue::Indirection {
        base,
        displacement: None,
    },
    "[" <base: Base>  <displacement: Displacement> "]" => LValue::Indirection {
        base,
        displacement: Some(displacement),
    },
};

Base: Base = {
    <reg: Reg> => Base::Reg(reg),
    <alias_name: Ident> => Base::Alias(alias_name),
};

Displacement: Displacement = {
    <l:@L> <signed: Signed> <r: @R> =>? {
        if (-512..512).contains(&signed) {
            Ok(Displacement::I10(signed))
        } else {
            Err(ParseError::User {
                error: UserError {
                    msg: format!("Offset immediate must fit in range -512..512. Received: {signed}"),
                    span: (l, r),
                }
            })
        }
    },
    "+" <konst: ConstIdent> => Displacement::Const(konst),
    "-" <konst: ConstIdent> => Displacement::NegatedConst(konst),
};

Integer: RValue = {
    <u: Unsigned> => RValue::Uint(u),
    <i: Signed> => RValue::Int(i),
};

Unsigned: u16 = {
    <n: r"0x[0-9a-fA-F]+"> => {
        let n = &n[2..].to_ascii_lowercase();
        return u16::from_str_radix(n, 16).expect("parse hexadecimal number");
    },
    <n: r"0o[01234567]+"> => {
        let n = &n[2..];
        return u16::from_str_radix(n, 8).expect("parse octal number");
    },
    <n: r"0b[01]+"> => {
        let n = &n[2..];
        return u16::from_str_radix(n, 2).expect("parse binary number");
    },
    <n: r"[0-9]+"> => n.parse().unwrap(),
};

Signed: i16 = {
    <n: r"(\+|\-)[0-9]+"> => {
        i16::from_str_radix(n, 10).expect("parse signed decimal number")
    },
    <l: @L> <n: r"(\+|\-)0x[0-9a-fA-F]+"> <r: @R> =>? {
        let sign = &n[..1];
        let digits = &n[3..].to_ascii_lowercase();
        let n = format!("{sign}{digits}");
        match i16::from_str_radix(&n, 16) {
            Err(e) => Err(ParseError::User {
                error: UserError {
                    msg: format!("Failed to parse signed hexadecimal number: `{n}` Cause: {:?}", e.kind()),
                    span: (l, r),
                }
            }),
            Ok(n) => Ok(n),
        }
    },
    <l: @L> <n: r"(\+|\-)0o[0-7]+"> <r: @R> =>? {
        let sign = &n[..1];
        let digits = &n[3..];
        let n = format!("{sign}{digits}");
        match i16::from_str_radix(&n, 8) {
            Err(e) => Err(ParseError::User {
                error: UserError {
                    msg: format!("Failed to parse signed octal number: `{n}` Cause: {:?}", e.kind()),
                    span: (l, r),
                }
            }),
            Ok(n) => Ok(n),
        }
    },
    <l: @L> <n: r"(\+|\-)0b[01]+"> <r: @R> =>? {
        let sign = &n[..1];
        let digits = &n[3..];
        let n = format!("{sign}{digits}");
        match i16::from_str_radix(&n, 2) {
            Err(e) => Err(ParseError::User {
                error: UserError {
                    msg: format!("Failed to parse signed binary number: `{n}` Cause: {:?}", e.kind()),
                    span: (l, r),
                }
            }),
            Ok(n) => Ok(n),
        }
    },
}

Reg: Reg = {
    r"\$zero" => Reg::Zero,
    r"\$rv" => Reg::Rv,
    r"\$ra" => Reg::Ra,
    r"\$a0" => Reg::A0,
    r"\$a1" => Reg::A1,
    r"\$a2" => Reg::A2,
    r"\$s0" => Reg::S0,
    r"\$s1" => Reg::S1,
    r"\$s2" => Reg::S2,
    r"\$t0" => Reg::T0,
    r"\$t1" => Reg::T1,
    r"\$t2" => Reg::T2,
    r"\$k0" => Reg::K0,
    r"\$k1" => Reg::K1,
    r"\$gp" => Reg::Gp,
    r"\$sp" => Reg::Sp,
};

Directive: Directive = {
    "[[addr(" <addr: Unsigned> ")]]" => Directive::Addr(addr),
    "[[data(" <l:@L> <first: RValue> <r:@R> <rest: ("," <@L> <RValue> <@R>)*> ")]]" =>? {
        let mut data = vec![];
        for (l, d, r) in [(l, first, r)].into_iter().chain(rest.into_iter()) {
            match d {
                RValue::ConstAlias(..) => data.push(d),
                RValue::Uint(uint) => if let Err(e) = u8::try_from(uint) {
                        return Err(ParseError::User {
                            error: UserError {
                                msg: format!("Data element `{uint}` must fit in a byte (0..=255)."),
                                span: (l, r),
                            }
                        });
                    } else {
                        data.push(d);
                    },
                ref other => return Err(ParseError::User {
                    error: UserError {
                        msg: format!("Data element `{d:?}` must be a `u8` or the name of a const."),
                        span: (l, r),
                    }
                }),
            }
        }
        Ok(Directive::Data(data))
    },
};